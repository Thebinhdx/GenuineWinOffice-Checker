@echo off

if not "%1" == "max" start /MAX cmd /c %0 max & exit/b

set "namebatch=GenuineWinOffice-Checker"
set "versionbatch=v1.3.1"

setlocal EnableDelayedExpansion

for /f "tokens=6 delims=[]. " %%a in ('ver') do set /a winbuild=%%a
set "nul1=>nul"
set "nul2=>nul"
set "ps=%SystemRoot%\System32\WindowsPowerShell\v1.0\powershell.exe"
set "psc=%ps% -NoLogo -NoProfile -ExecutionPolicy Bypass -Command"

set "_NCS=1"
if %winbuild% LSS 10586 set "_NCS=0"
if %winbuild% GEQ 10586 reg query "HKCU\Console" /v ForceV2 %nul2% | find /i "0x0" %nul1% && (set "_NCS=0")

if %_NCS% EQU 1 (
for /F %%a in ('echo prompt $E ^| cmd') do set "esc=%%a"
set     "Red="41;97m""
set    "Gray="100;97m""
set   "Green="42;97m""
set    "Blue="44;97m""
set   "White="107;91m""
set    "_Red="40;91m""
set  "_White="40;37m""
set  "_Green="40;92m""
set "_Yellow="40;93m""
) else (
set     "Red="Red" "white""
set    "Gray="Darkgray" "white""
set   "Green="DarkGreen" "white""
set    "Blue="Blue" "white""
set   "White="White" "Red""
set    "_Red="Black" "Red""
set  "_White="Black" "Gray""
set  "_Green="Black" "Green""
set "_Yellow="Black" "Yellow""
)

echo Checking Administrator Requirements...

net session >nul 2>&1
if %errorlevel% == 0 (
  goto continue
) else (
  cls
  call :dk_color %Red% "===Error==="
  echo Administrator privileges required.  Please run this script as administrator.
  echo Press Any Key to Exit...
  pause >nul
  exit /b 1
)

:continue
cls
if %winbuild% LSS 19043 (
%eline%
call :dk_color %Red% "===Error==="
echo Unsupported OS version detected [%winbuild%].
echo Error Code: 1207
echo:
call :dk_color %Blue% "Only supported on Windows 10 [21H1]/11."
echo Press Any Key to Exit...
pause >nul
exit /b 1
)


:check_product
cls
echo Checking computer information / Kiem tra thong tin may...
timeout /t 2 /nobreak >nul

set "PC_NAME=%COMPUTERNAME%"
set "CURRENT_USER=%USERNAME%"

for /f "tokens=2*" %%A in ('reg query "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion" /v ProductName 2^>nul') do set "OS_Name=%%B"
for /f "tokens=2*" %%A in ('reg query "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion" /v DisplayVersion 2^>nul') do set "OS_Version=%%B"

set "OS_NAME=%OS_Name%"
set "VERSION=%OS_Version%"

for /f "tokens=3" %%A in ('reg query "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion" /v EditionID 2^>nul') do (
    set "EDITION=%%A"
)

cscript //nologo %systemroot%\system32\slmgr.vbs /xpr | findstr /i "permanently" >nul

if %errorlevel% equ 0 (
    set "WIN_ACT=Activated / Da kich hoat"
) else (
    cscript //nologo %systemroot%\system32\slmgr.vbs /xpr | findstr /i "grace" >nul
    if !errorlevel! equ 0 (
        set "WIN_ACT=[OOB Grace] Trial Time / Dang trong thoi gian dung thu"
    ) else (
        set "WIN_ACT=Unactivated / Chua kich hoat hoac loi"
    )
)


if defined PROCESSOR_ARCHITEW6432 (
    set "ARCH=x64"
) else (
    if /i "%PROCESSOR_ARCHITECTURE%"=="AMD64" (
        set "ARCH=x64"
    ) else if /i "%PROCESSOR_ARCHITECTURE%"=="ARM64" (
        set "ARCH=ARM64"
    ) else (
        set "ARCH=x86"
    )
)


set "LTSC=No"

echo %OS_NAME% | find /i "LTSC" >nul && set "LTSC=Yes"

echo %OS_NAME% | find /i "IoT Enterprise LTSC" >nul && set "LTSC=Yes"

set "EVALUATION=No"

echo %OS_NAME% | find /i "Evaluation" >nul && set "EVALUATION=Yes"

set "office_installed=0"
set "office_activated=0"
set "office_vbs="

for %%A in (
    "C:\Program Files\Microsoft Office\Office16\ospp.vbs"
    "C:\Program Files (x86)\Microsoft Office\Office16\ospp.vbs"
    "C:\Program Files\Microsoft Office\Office15\ospp.vbs"
    "C:\Program Files (x86)\Microsoft Office\Office15\ospp.vbs"
    "C:\Program Files\Microsoft Office\Office14\ospp.vbs"
    "C:\Program Files (x86)\Microsoft Office\Office14\ospp.vbs"
) do (
    if exist "%%~A" set "office_vbs=%%~A"
)

if not defined office_vbs (
    for %%B in (
        "C:\Program Files\Microsoft Office\root\Office16\ospp.vbs"
        "C:\Program Files (x86)\Microsoft Office\root\Office16\ospp.vbs"
    ) do (
        if exist "%%~B" set "office_vbs=%%~B"
    )
)

if defined office_vbs (
    set "office_installed=1"
    set "temp_off=%temp%\office_check.txt"
    
    cscript //nologo "!office_vbs!" /dstatus > "!temp_off!" 2>nul
    
    findstr /i /c:"---LICENSED---" "!temp_off!" >nul && set "office_activated=1"
    
    if exist "!temp_off!" del /f /q "!temp_off!"
)

set "skip_check_office=0"
set "OFF_ACT=unknown"

if "%office_installed%"=="1" (
    set "OFFINSTALLED=Installed / Da cai dat"
    set "skip_check_office=0"
    if "%office_activated%"=="1" (
	set "OFF_ACT=Activated / Da kich hoat"
    ) else (
	set "OFF_ACT=Unactivated / Chua kich hoat"
    )
) else (
    set "OFFINSTALLED=Not Installed / Chua cai dat"
    set "skip_check_office=1"
)

for /f "delims=" %%A in ('powershell -Command "[timezone]::CurrentTimeZone.ToLocalTime(([datetime]'1/1/1970').AddSeconds((Get-ItemProperty 'HKLM:\Software\Microsoft\Windows NT\CurrentVersion').InstallDate))"') do set "Install_Date=%%A"

set "TIME=%Install_Date%"

cls
title GenuineWinOffice-Checker
echo ===================-Computer Information / Thong tin may:-===================
echo:
echo Computer name / Ten may: %PC_NAME%
echo User / Nguoi dung: %CURRENT_USER%
echo:
echo Windows:
echo:
echo OS / He dieu hanh: %OS_NAME% 
echo Edition / Phien ban: %EDITION%
echo Version / Phien ban: %VERSION%
echo Architecture / Kien truc: %ARCH%
echo LTSC: %LTSC%
echo Evaluation: %EVALUATION%
echo Installed On / Thoi gian cai dat: %TIME%
call :dk_color2 %_White% "Activation status / Tinh trang kich hoat: " %_Yellow% "%WIN_ACT%"
echo:
echo Office:
echo:
call :dk_color2 %_White% "Status / Tinh trang: " %_Yellow% "%OFFINSTALLED%"
call :dk_color2 %_White% "Activation status / Tinh trang kich hoat: " %_Yellow% "%OFF_ACT%"
echo:
call :dk_color %Blue% "[i] Just because Office says its not activated doesnt mean it hasnt been cracked."
echo:
echo =============================================================================
echo Name: %namebatch%
echo Version: %versionbatch%
echo:
echo Deep Scanning will start in 5 seconds / Kiem tra sau se bat dau sau 5 giay...
timeout /t 5 /nobreak >nul

cls
echo Scanning / Dang quet...
timeout /t 2 /nobreak >nul
cls
call :dk_color %Blue% "Checking Windows license / Kiem tra giay phep Windows:"
echo:
echo Checking the KMS server. / Kiem tra may chu KMS.
set "KMS=false"
set "kms_host=None"
set "kms_verdict=clean"

cscript //nologo %systemroot%\system32\slmgr.vbs /dli | findstr /i "VOLUME_KMS VOLUME" >nul
if %errorlevel% equ 0 (
    :: Lấy địa chỉ máy chủ KMS
    for /f "tokens=3" %%K in ('cscript //nologo %systemroot%\system32\slmgr.vbs /dli ^| findstr /i "KMS machine name"') do (
        set "temp_kms=%%K"
        for /f "tokens=1 delims=:" %%L in ("!temp_kms!") do set "kms_host=%%L"
    )
    
    if defined kms_host set "kms_host=!kms_host: =!"
    
    if defined kms_host (
        set "kms_blacklist=127.0.0.1 localhost kms.msganti.com kms.digiboy.ir kms.loli.best mskms.orgzh.org kms.lotro.cc kms.chinancce.com kms.shuax.com"
        
        set "is_black=0"
        for %%B in (!kms_blacklist!) do (
            if /i "!kms_host!"=="%%B" set "is_black=1"
        )
        
        set "is_trusted_domain=0"
        set "is_private_ip=0"
        
        if "!is_black!"=="0" (
            echo !kms_host! | findstr /i "\.edu \.edu\.vn \.gov \.gov\.vn \.org" >nul
            if !errorlevel! equ 0 set "is_trusted_domain=1"
            
            echo !kms_host! | findstr /r "^10\." >nul && set "is_private_ip=1"
            echo !kms_host! | findstr /r "^192\.168\." >nul && set "is_private_ip=1"
            echo !kms_host! | findstr /r "^172\.\(1[6-9]\|2[0-9]\|3[0-1]\)\." >nul && set "is_private_ip=1"
        )
        
        if "!is_black!"=="1" (
            set "KMS=true"
            set "kms_verdict=suspicious"
        ) else if "!is_trusted_domain!"=="1" (
            set "KMS=false"
            set "kms_verdict=trusted_domain"
        ) else if "!is_private_ip!"=="1" (
            set "KMS=false"
            set "kms_verdict=private_network"
        ) else (
            set "KMS=true"
            set "kms_verdict=unknown_server"
        )
    )
)
echo Checking KMS38. / Kiem tra KMS38.
call :dk_color %_Yellow% "In Windows Build 26100.7019, Microsoft removed several features that caused KMS38 to stop working."

if %winbuild% GEQ 26100.7019 (
%eline%
call :dk_color %Blue% "[i] Your OS version not support KMS38 activation."
set "KMS38=not_supported"
goto skip_kms38
)

set "KMS38=unknown"
for /f "tokens=*" %%T in ('cscript //nologo %systemroot%\system32\slmgr.vbs /xpr 2^>nul') do set "expire_info=%%T"
echo !expire_info! | findstr /i "2038" >nul
if %errorlevel% equ 0 (
    set "KMS38=true"
)

:skip_kms38

echo Checking HWID. / Kiem tra HWID.

set "hwid=unknown"
set "current_partial_key="
for /f "tokens=2 delims=:" %%A in ('cscript //nologo %systemroot%\system32\slmgr.vbs /dli ^| findstr /i "Partial Product Key"') do (
    set "temp_key=%%A"
    set "current_partial_key=!temp_key: =!"
)

set "generic_keys=7CFBY DRR8H 8HV2C QPFCT MDWWW DYJWX P39PB M7V2X 9HKR4 8HVX7 WXCHW 8TYMD 6F4BT CKFFD RRK69 YY74H J8JXD D32MH 3V66T PKCKT MHBPB QPF8P 2YV77 WT2RQ VMJ2C DJ4F6 T6R4W BHDCD KD72Y"

set "is_hwid_mas=0"
if defined current_partial_key (
    for %%K in (%generic_keys%) do (
        if /i "%current_partial_key%"=="%%K" (
            set "is_hwid_mas=1"
        )
    )
)

if "%is_hwid_mas%"=="1" (
    set "hwid=true"
) else (
    set "hwid=false"
)

echo Checking the copyright logic and OEM key.  / Kiem tra logic ban quyen va OEM key.
set "bios=unknown"

set "active_partial_key="
for /f "tokens=3 delims=: " %%A in ('cscript //nologo %systemroot%\system32\slmgr.vbs /dli ^| findstr /i "Partial Product Key"') do (
    set "active_partial_key=%%A"
)

set "bios_key="
for /f "tokens=2*" %%A in ('reg query "HKLM\SYSTEM\CurrentControlSet\Control\ProductOptions" /v "OriginalProductKey" 2^>nul') do (
    set "bios_key=%%B"
)

if not defined bios_key (
    for /f "tokens=2*" %%A in ('reg query "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform" /v "BackupProductKeyDefault" 2^>nul') do (
        set "bios_key=%%B"
    )
)

if "%bios_key%"=="" (
    set "bios=unknown"
) else (
    set "bios_partial_key=!bios_key:~-5!"
    
    if defined active_partial_key (
        if /i "!active_partial_key!"=="!bios_partial_key!" (
            set "bios=false"
        ) else (
            if "%is_hwid_mas%"=="1" (
                set "bios=upgrade"
            ) else (
                set "bios=false1"
            )
        )
    )
)

echo Checking the suspicious folder or file. / Kiem tra thu muc hay file nghi van.

set "kmsfile=unknown"
set "kms_tool_detected=0"
set "detected_paths="

set "target_paths="C:\Program Files\KMSpico" "C:\Program Files (x86)\KMSpico" "C:\ProgramData\KMSAuto" "C:\ProgramData\KMSAutoS" "C:\Windows\KMSAuto" "C:\Program Files\KMSAuto Net""

for %%P in (%target_paths%) do (
    if exist %%P (
        set "kms_tool_detected=1"
        set "detected_paths=!detected_paths! %%P,"
    )
)

set "target_files="C:\Windows\SECOH-QAD.exe" "C:\Windows\SECOH-QAD.dll" "C:\Windows\KMSConnection.xml""
for %%F in (%target_files%) do (
    if exist %%F (
        set "kms_tool_detected=1"
        set "detected_paths=!detected_paths! %%F,"
    )
)

sc query "KMSpico_service" >nul 2>&1
if %errorlevel% equ 0 (
    set "kms_tool_detected=1"
    set "detected_paths=!detected_paths! [Service: KMSpico_service],"
)
sc query "Service_KMS" >nul 2>&1
if %errorlevel% equ 0 (
    set "kms_tool_detected=1"
    set "detected_paths=!detected_paths! [Service: Service_KMS],"
)

if "%kms_tool_detected%"=="1" (
    set "kmsfile=true"
) else (
    set "kmsfile=false"
)

echo Checking the KMS Tasks. / Kiem tra cac task KMS.

set "kmstask=unknown"
set "task_detected=0"
set "detected_tasks="

set "target_tasks="KMSConnection" "KMSpico" "KMSAuto" "KMSAutoS" "KMS38" "Wub" "KMS-Activation" "HEU_KMS" "AIO_KMS""

for %%T in (%target_tasks%) do (
    schtasks /query /fo LIST 2>nul | findstr /i /c:"TaskName: \%%T" /c:"TaskName: \Microsoft\Windows\%%T" >nul
    if !errorlevel! equ 0 (
        set "task_detected=1"
        set "detected_tasks=!detected_tasks! [Task: %%T],"
    )
)

set "temp_tasks=%temp%\tasks_list.txt"
schtasks /query /v /fo CSV > "%temp_tasks%" 2>nul

findstr /i "secoh-qad" "%temp_tasks%" >nul && (
    set "task_detected=1"
    set "detected_tasks=!detected_tasks! [Task: Chay SECOH-QAD],"
)
findstr /i "Appdata\Local\Temp" "%temp_tasks%" | findstr /i ".vbs .bat .ps1" >nul && (
    set "task_detected=1"
    set "detected_tasks=!detected_tasks! [Task: Chay Script tu Temp],"
)

if exist "%temp_tasks%" del /f /q "%temp_tasks%"

if "%task_detected%"=="1" (
    set "kmstask=true"
) else (
    set "kmstask=false"
)

echo Checking for registry interference. / Dang kiem tra can thiep registry.

set "registry=unknown"
set "reg_bypass_detected=0"
set "detected_regs="

reg query "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform" /v NoGenTicket >nul 2>&1
if %errorlevel% equ 0 (
    set "reg_bypass_detected=1"
    set "detected_regs=!detected_regs! [Registry: NoGenTicket Bypass],"
)

reg query "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform" /v KeyManagementServiceName >nul 2>&1
if %errorlevel% equ 0 (
    for /f "tokens=3" %%R in ('reg query "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform" /v KeyManagementServiceName 2^>nul') do set "kms_reg_ip=%%R"
    set "reg_bypass_detected=1"
    set "detected_regs=!detected_regs! [KMS Server Registry: !kms_reg_ip!],"
)

reg query "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform" /v KeyManagementServicePort >nul 2>&1
if %errorlevel% equ 0 (
    for /f "tokens=3" %%P in ('reg query "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform" /v KeyManagementServicePort 2^>nul') do set "kms_reg_port=%%P"
    set "reg_bypass_detected=1"
    set "detected_regs=!detected_regs! [KMS Port Registry: !kms_reg_port!],"
)

if "%reg_bypass_detected%"=="1" (
    set "registry=true"
) else (
    set "registry=false"
)

echo Checking console history. / Kiem tra lich su console.
set "ps_history_file=%APPDATA%\Microsoft\Windows\PowerShell\PSReadLine\ConsoleHost_history.txt"
set "found_history=0"

if exist "%ps_history_file%" (
    set "scan=true"
    findstr /i /c:"get.activated.win" /c:"massgrave" /c:"irm https://get.activated.win | iex" "%ps_history_file%" > "%temp%\ps_history_matches.txt" 2>nul
    
    for %%F in ("%temp%\ps_history_matches.txt") do (
        if %%~zF GTR 0 set "found_history=1"
    )
) else (
    echo:
    call :dk_color %Red% "Error: Scanning failed / Quet that bai"
    set "scan=fail"
)

echo:
call :dk_color %Blue% "Checking Office license / Kiem tra giay phep Office:"

echo:
echo Checking Office 2010-365 GVLK/KMS license. / Kiem tra giay phep Office 2010-365 GVLK/KMS license.

set "office_status=Not Installed"
set "office_is_crackkms=unknown"
set "office_is_crackohook=unknown"
set "office_vbs="
set "is_kms_detected=0"

for %%A in (
    "C:\Program Files\Microsoft Office\Office16\ospp.vbs"
    "C:\Program Files (x86)\Microsoft Office\Office16\ospp.vbs"
    "C:\Program Files\Microsoft Office\Office15\ospp.vbs"
    "C:\Program Files (x86)\Microsoft Office\Office15\ospp.vbs"
    "C:\Program Files\Microsoft Office\Office14\ospp.vbs"
    "C:\Program Files (x86)\Microsoft Office\Office14\ospp.vbs"
) do (
    if exist "%%~A" set "office_vbs=%%~A"
)

if not defined office_vbs (
    for %%B in (
        "C:\Program Files\Microsoft Office\root\Office16\ospp.vbs"
        "C:\Program Files (x86)\Microsoft Office\root\Office16\ospp.vbs"
    ) do (
        if exist "%%~B" set "office_vbs=%%~B"
    )
)

if defined office_vbs (
    set "office_status=Unactivated"
    set "temp_off=%temp%\office_status.txt"
    
    cscript //nologo "!office_vbs!" /dstatus > "!temp_off!" 2>nul
    
    findstr /i /c:"---LICENSED---" "!temp_off!" >nul && set "office_status=Activated"
    
    findstr /i /c:"KMS_Client GVLK Mondo" "!temp_off!" >nul && (
        set "is_kms_detected=1"
        
        set "kms_server="
        for /f "tokens=2* delims=:" %%K in ('findstr /i /c:"KMS machine name" "!temp_off!"') do (
            set "kms_server=%%K"
            set "kms_server=!kms_server: =!"
        )
        
        if /i "!kms_server!"=="127.0.0.1" set "office_is_crackkms=1"
        if /i "!kms_server!"=="localhost" set "office_is_crackkms=1"
        if /i "!kms_server!"=="127.0.0.2" set "office_is_crackkms=1"
        if /i "!kms_server!"=="0.0.0.0"   set "office_is_crackkms=1"
        if /i "!kms_server!"=="::1"       set "office_is_crackkms=1"

        set "kms_blacklist=msguides.com digiboy.ir loli.net cucg.me chinancce.com windows.net.cn kms.space kms.pub kms.xin kms.lol kms.cat kms.moe sytes.net no-ip.org kms.host kms.win kms.one mskms.ru kms.xyz kms.info kms.tools kms.work cyb7.io"
        
        for %%S in (!kms_blacklist!) do (
            echo !kms_server! | findstr /i "%%S" >nul && set "office_is_crackkms=1"
        )
    )
    
    if exist "!temp_off!" del /f /q "!temp_off!"
)

echo:

echo Checking Office 2013-365 Ohook license. / Kiem tra giay phep Office 2013-365 Ohook license.
call :dk_color %_Yellow% "Ohook only support Office support C2R installers. / Kich hoat Ohook chi ho tro phien ban office ho tro bo tai C2R."
set "office_is_ohook=0"
set "ohook_static_paths="C:\Program Files\Microsoft Office 15\root\vfs\System" "C:\Program Files (x86)\Microsoft Office 15\root\vfs\System" "C:\Program Files\Microsoft Office\root\vfs\System" "C:\Program Files (x86)\Microsoft Office\root\vfs\System" "C:\Program Files\Microsoft Office 16\root\vfs\System" "C:\Program Files (x86)\Microsoft Office 16\root\vfs\System""

for %%D in (%ohook_static_paths%) do (
    if exist "%%~D\sppcs.dll" (
        set "office_is_ohook=1"
    )
)

if "%office_is_ohook%"=="1" (
    set "office_is_crackohook=1"
)

echo:

call :dk_color %Green% "Scanning successful. / Quet thanh cong."
timeout /t 3 /nobreak >nul
cls
call :dk_color %_Yellow% "Outputting results. / Dang xuat ket qua."
timeout /t 2 /nobreak >nul
cls



cls
echo =============================-Results / Ket qua-=============================
echo:
echo Computer name / Ten may: %PC_NAME%
echo User / Nguoi dung: %CURRENT_USER%
echo Date of results/ Ngay xuat ket qua: %time%
echo:
echo =============================================================================
echo:
echo Windows license check results:
echo:
if /i "%KMS%"=="true" (
    if /i "%kms_verdict%"=="suspicious" (
	call :dk_color %_Red% "[-] Traces of activation of a pirated KMS server have been detected."
    	call :dk_color %_Red% "[-] Phat hien dau vet kich hoat may chu KMS lau."
    ) else if /i "%kms_verdict%"=="trusted_domain" (
    	call :dk_color %_Green% "[+] No traces of pirated KMS were detected."
    	call :dk_color %_Green% "[+] Ko phat hien dau vet KMS lau."
    ) else if /i "%kms_verdict%"=="private_network" (
    	call :dk_color %_Green% "[+] No traces of pirated KMS were detected."
    	call :dk_color %_Green% "[+] Ko phat hien dau vet KMS lau."
    ) else if /i "%kms_verdict%"=="unknown_server" (
	call :dk_color %_Red% "[-] Traces of activation of a pirated KMS server have been detected."
    	call :dk_color %_Red% "[-] Phat hien dau vet kich hoat may chu KMS lau."
    )
) else (
    call :dk_color %_Green% "[+] No traces of pirated KMS were detected."
    call :dk_color %_Green% "[+] Ko phat hien dau vet KMS lau."
)

echo:

if /i "%KMS38%"=="true" (
    call :dk_color %_Red% "[-] Traces of activation of a pirated KMS38 server have been detected."
    call :dk_color %_Red% "[-] Phat hien dau vet kich hoat may chu KMS38 lau."
) else if /i "%KMS38%"=="unknown" (
    call :dk_color %_Green% "[+] No traces of pirated KMS38 were detected."
    call :dk_color %_Green% "[+] Ko phat hien dau vet KMS38 lau."
) else if /i "%KMS38%"=="not_supported" (
    call :dk_color %_Yellow% "[ ] Your OS version is not supported."
)

echo:

if /i "%hwid%"=="true" (
    call :dk_color %_Red% "[-] Detecting activation traces using HWID."
    call :dk_color %_Red% "[-] Phat hien dau vet kich hoat bang HWID."
) else (
    call :dk_color %_Green% "[+] No evidence of using pirated HWID was detected."
    call :dk_color %_Green% "[+] Ko phat hien dau vet dung hwid lau."
)

echo:

if /i "%bios%"=="false" (
    call :dk_color %_Green% "[+] BIOS Key matches."
    call :dk_color %_Green% "[+] BIOS Key trung khop."
)
if /i "%bios%"=="unknown" (
    call :dk_color %_Yellow% "[ ] BIOS key not found."
    call :dk_color %_Yellow% "[ ] Khong tim thay key BIOS."
)
if /i "%bios%"=="upgrade" (
    call :dk_color %Blue% "[i] Upgrade key detected via Generic Digital License."
    call :dk_color %Blue% "[i] Key nang cap duoc phat hien thong qua giay phep so Generic."
)
if /i "%bios%"=="false1" (
    call :dk_color %_Green% "[+] Independent Retail/Volume key detected [Different from BIOS]."
    call :dk_color %_Green% "[+] Phat hien key Retail/Volume doc lap [Khac voi key BIOS]."
)

echo:

if /i "%kmsfile%"=="true" (
    call :dk_color %_Red% "[-] Suspicious folders and files detected."
    call :dk_color %_Red% "[-] Phat hien thu muc va file dang ngo."
) else (
    call :dk_color %_Green% "[+] No suspicious folders or files were detected."
    call :dk_color %_Green% "[+] Khong phat hien thu muc va file dang ngo."
)

echo:

if /i "%kmstask%"=="true" (
    call :dk_color %_Red% "[-] The suspicious task has been detected."
    call :dk_color %_Red% "[-] Phat hien task dang ngo."
) else (
    call :dk_color %_Green% "[+] No suspicious tasks detected."
    call :dk_color %_Green% "[+] Khong phat hien task dang ngo."
)

echo:

if /i "%registry%"=="true" (
    call :dk_color %_Red% "[-] Registry tampering has been detected."
    call :dk_color %_Red% "[-] Phat hien registry bi can thiep."
) else (
    call :dk_color %_Green% "[+] No registry interference detected."
    call :dk_color %_Green% "[+] Khong phat hien su can thiep registry."
)

echo:

if /i "%scan%"=="true" (
    if /i "%found_history%"=="1" (
        call :dk_color %_Red% "[-] Traces of console usage history discovered."
        call :dk_color %_Red% "[-] Phat hien dau vet lich su console xai crack."
    ) else (
	call :dk_color %_Green% "[+] No traces of console usage history found."
    	call :dk_color %_Green% "[+] Khong phat hien dau vet lich su console xai crack."
    )
) else if /i "%scan%"=="fail" (
    call :dk_color %_Yellow% "[ ] Error during console history check."
    call :dk_color %_Yellow% "[ ] Loi trong qua trinh kiem tra lich su console."
)

echo:

echo Office license check results:
echo:

if /i "%office_status%"=="Not Installed" (
    call :dk_color %_Yellow% "[ ] Office is not installed."
    call :dk_color %_Yellow% "[ ] Office chua duoc cai dat."
) else (
    if /i "%office_is_crackkms%"=="1" (
	call :dk_color %_Red% "[-] Office activated via KMS has been detected."
	call :dk_color %_Red% "[-] Phat hien Office kich hoat bang KMS."
    ) else if /i "%office_is_crackkms%"=="unknown" (
	call :dk_color %_Green% "[+] No signs of pirated Office activation using KMS were detected."
	call :dk_color %_Green% "[+] Khong phat hien dau hieu kich hoat lau office bang KMS."
    )
)

echo:

if /i "%office_status%"=="Not Installed" (
    call :dk_color %_Yellow% "[ ] Office is not installed."
    call :dk_color %_Yellow% "[ ] Office chua duoc cai dat."
) else if /i "%office_is_crackohook%"=="1" (
    call :dk_color %_Red% "[-] Office activated via Ohook has been detected."
    call :dk_color %_Red% "[-] Phat hien Office kich hoat bang Ohook."
    call :dk_color %Blue% "[i] The information on the computer indicates that Office is not activated because this activation method requires file manipulation."
) else if /i "%office_is_crackohook%"=="unknown" (
    call :dk_color %_Green% "[+] No signs of pirated Office activation using Ohook were detected."
    call :dk_color %_Green% "[+] Khong phat hien dau hieu kich hoat lau office bang Ohook."
) 

:: Logic phan tich windows
set "windows=1"

:: 1 Good / Tot
:: 2 Warning / Canh bao
:: 3 Violate / Vi pham

if /i "%kmsfile%"=="true" set "windows=2"
if /i "%kmstask%"=="true" set "windows=2"
if /i "%registry%"=="true" set "windows=2"
if /i "%found_history%"=="1" set "windows=2"
if /i "%KMS%"=="true" set "windows=3"
if /i "%KMS38%"=="true" set "windows=3"
if /i "%hwid%"=="true" set "windows=3"

:: Logic phan tich Office
set "office=1"

:: 1 Good / Tot
:: 2 Warning / Canh bao
:: 3 Violate / Vi pham

if /i "%office_is_crackkms%"=="1" set "office=3"
if /i "%office_is_crackohook%"=="1" set "office=3"

echo:

echo Final Verdict/ Ket luan chung:
echo:
echo Windows:
if /i "%windows%"=="3" (
    call :dk_color %_Red% "[-] Illegal activation detected / Da phat hien kich hoat lau:"
    echo:
    call :dk_color %_Yellow% "The computer is being used with cracks and various other methods to bypass the license and illegally use the operating system."
    call :dk_color %_Yellow% "May tinh dang dung crack va cac cach khac nhau [KMS, HWID, KMS38] de be khoa ban quyen va su dung trai phep he dieu hanh."
) else if /i "%windows%"=="2" (
    call :dk_color %_Yellow% "[*] Warning / Canh bao:"
    echo:
    call :dk_color %_Yellow% "No instances of illegal Windows activation were detected, but there were signs of previous use of cracking tools."
    call :dk_color %_Yellow% "Khong phat hien viec kich hoat lau windows nhung co dau hieu tung su dung cac cong cu crack."
) else if /i "%windows%"=="1" (
    call :dk_color %_Green% "[+] Genuine legal activation / Kich hoat hop phap chinh hang:"
    echo:
    call :dk_color %_Yellow% "The system license is fully compliant. No bypass methods, unauthorized KMS servers, or licensing exploits were detected."
    call :dk_color %_Yellow% "Ban quyen he thong hoan toan hop le. Khong phat hien phuong thuc bypass, may chu KMS trai phep hoac can thiep giay phep."
    echo:
    call :dk_color %Blue% "[i] This system complies with Microsoft Licensing Terms / Thiet bi nay tuan thu dung Dieu khoan ban quyen cua Microsoft."
)
echo:
echo Office:
if /i "%office%"=="3" (
    call :dk_color %_Red% "[-] Illegal activation detected / Da phat hien kich hoat lau:"
    echo:
    call :dk_color %_Yellow% "The computer is using the [KMS, Ohook] method to crack and illegally use Office."
    call :dk_color %_Yellow% "May tinh dang dung phuong thuc [KMS, Ohook] de be khoa va su dung trai phep Office."
) else if /i "%office%"=="1" (
    call :dk_color %_Green% "[+] Genuine legal activation / Kich hoat hop phap chinh hang:"
    echo:
    call :dk_color %_Yellow% "The copyright is completely legitimate. No cracking methods, KMS servers, or license interference were detected."
    call :dk_color %_Yellow% "Ban quyen hoan toan hop le. Khong phat hien cac phuong thuc be khoa, may chu KMS hoac su can thiep giay phep."
)

echo:
call :dk_color %Blue% "[i] The results are from an automated check and are not guaranteed to be accurate; please check manually for more details."
echo:
echo Press any key to exit...
pause >nul
exit


:dk_color
if %_NCS% EQU 1 (
  echo %esc%[%~1%~2%esc%[0m
) else (
  if exist "%ps%" (
    %psc% write-host -back '%1' -fore '%2' '%3'
  ) else (
    echo %~3
  )
)
exit /b

:dk_color2
if %_NCS% EQU 1 (
  echo %esc%[%~1%~2%esc%[%~3%~4%esc%[0m
) else (
  if exist "%ps%" (
    %psc% write-host -back '%1' -fore '%2' '%3' -NoNewline; write-host -back '%4' -fore '%5' '%6'
  ) else (
    echo %~3 %~6
  )
)
exit /b

:: LEAVE EMPTY BLANK HERE